buildscript {
	ext {
		grailsVersion = project.grailsVersion
	}
	repositories {
		mavenLocal()
		maven { url 'https://repo.grails.org/grails/core' }
		jcenter()
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
		classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
		classpath 'org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.4'
		classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.9'
	}
}

plugins {
	id 'io.spring.dependency-management' version '0.5.2.RELEASE'
	id 'com.jfrog.bintray' version '1.2'
}

version '3.0.0.M1'
group 'org.grails.plugins'

apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.grails.grails-plugin'
apply plugin: 'org.asciidoctor.convert'

String repoRoot = 'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates'
apply from: "$repoRoot/grailsCentralPublishing.gradle"
//apply from: "$repoRoot/bintrayPublishing.gradle"
apply from: 'bintrayPublishing.gradle' // using modified local copy so the issueTrackerUrl and vcsUrl properties are correct

ext {
	grailsVersion = project.grailsVersion
	gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = targetCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://repo.grails.org/grails/core' }
}

dependencyManagement {
	imports {
		mavenBom "org.grails:grails-bom:$grailsVersion"
	}
	applyMavenExclusions false
}

dependencies {

	provided 'javax.servlet:javax.servlet-api:3.1.0'
	provided 'org.grails:grails-dependencies'
	provided 'org.grails:grails-web-boot'
	provided 'org.springframework.boot:spring-boot-autoconfigure'

	compile 'org.grails.plugins:spring-security-core:3.0.0.M1'

	String springSecurityVersion = '3.2.8.RELEASE'
	compile "org.springframework.security:spring-security-acl:$springSecurityVersion", {
		['aopalliance', 'commons-logging', 'ehcache-core', 'fest-assert', 'hsqldb', 'jcl-over-slf4j', 'junit',
		 'logback-classic', 'mockito-core', 'spring-aop', 'spring-beans', 'spring-context', 'spring-context-support',
		 'spring-core', 'spring-jdbc', 'spring-security-core', 'spring-test', 'spring-tx'].each { exclude module: it }
	}

	console 'org.grails:grails-console'

	testCompile 'org.grails:grails-plugin-testing'
}

asciidoctor {
	separateOutputDirs = false
	sourceDir = file('src/docs')
	sources {
		include 'index.adoc'
	}
	outputDir file('build/docs')
	backends 'html5', 'pdf', 'epub3'
	attributes 'source-highlighter': 'prettify',
			icons:               'font',
			setanchors:          'true',
			idprefix:            '',
			idseparator:         '-',
			toc2:                '',
			numbered:            '',
			revnumber:           project.version
}

task docs(dependsOn: asciidoctor) << {
	File dir = file('build/docs')

	['pdf', 'epub'].each { String ext ->
		File f = new File(dir, 'index.' + ext)
		if (f.exists()) {
			f.renameTo new File(dir, project.name + '-' + project.version + '.' + ext)
		}
	}

	file('build/docs/ghpages.html') << file('src/docs/index.tmpl').text.replaceAll('@VERSION@', project.version)
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleWrapperVersion
}

eclipse {
	classpath {
		plusConfigurations += [configurations.provided]
	}
}

idea {
	module {
		scopes.PROVIDED.plus += [configurations.provided]
	}
}
